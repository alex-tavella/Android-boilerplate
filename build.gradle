// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath Deps.tools_gradleandroid
        classpath Deps.tools_kotlin
        classpath Deps.tools_gradleversions
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                packagingOptions {
                    exclude 'META-INF/LICENSE*'
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }
            }

            dependencies {
                implementation(Deps.androidx_core)
                implementation(Deps.androidx_viewmodel_ktx)
                implementation(Deps.androidx_lifecycle)
                implementation(Deps.coroutines)
                implementation(Deps.androidx_material)
                implementation(Deps.androidx_constraintlayout)

                testImplementation Deps.testlib_junit
                testImplementation Deps.kotest

                androidTestImplementation Deps.testandroidx_runner
                androidTestImplementation Deps.testandroidx_rules
                androidTestImplementation(Deps.testandroidx_espressocore)
                androidTestImplementation Deps.kotest
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
